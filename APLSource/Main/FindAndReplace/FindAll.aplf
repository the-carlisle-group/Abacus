 FindAll←{
     d←⍵
     p←A.GetComponentValues d
     g←d.DataGrid
     t←g.Table
     s←g.Table.Selection
     rg←⊃d A.GetElementsByClass'data-grid'      ⍝ Result grid on dialog box
     ⍝ 0=≢p.FindWhat~' ':1
     _←p ⎕VSET(≢/s)/⊂'Selection's ⍝ Only if
     p.RegEx←GetSearchPattern p
     ⍝ p.RegExReplace←?
     j←⎕JSON p
     (g.Table.LastChangeTime<d.FindTime)∧j≡d.LastSettings:0
     _←rg A.SetStyle'display' 'none'
     d.LastSettings←j
     p.(RowMask ColumnMask)←(≡/s)∨t.DataSize↑¨{0 1/⍨(⊃⍵),1+-/⌽⍵}¨↓⍉↑s
     p.Columns←g.Table.Columns
     d.FindTime←2 ⎕DT⊂⎕TS
     d.SearchAxis←p.SearchBy≡'Columns'
     n←p FindEach¨⍳≢g.Table.Columns
     l←n ListFoundCells¨g.Table.Columns
     l←⊃⍪/l
     d.Found←n       ⍝ Maybe not boolean,
     d.FoundMessage←SpellResults n
     d.FoundCells←⍸(⍉⍣(~d.SearchAxis))↑n≠0
     0=≢l:1⊣d.Document A.Alert'Nothing found'
     c←()()()
     c.Name←'Column' 'Row' 'Value'
     c.Type←'VarChar' 'Int' 'VarChar'
     c.Value←↓⍉{⍵:l ⋄ l[⍋l[;1];]}d.SearchAxis
     c[(~d.SearchAxis)⌽⍳2]←c[⍳2]
     ⍝ rg←⊃d A.GetElementsByClass'data-grid'      ⍝ Result grid on dialog box
     rg.Table.RowNumbers←1
     rg.Table.Columns←c
     _←A.DataGrid.Init rg
     _←rg A.DataGrid.Resize 0
     _←rg A.SetStyle'display' 'grid'
     0⊣d.Document A.Alert d.FoundMessage
     0
 }
