 OnKeyDown←{
     k←⍵.Key
     e←⍵.CurrentTarget ⍝ Input element
     p←e.Options
     k≡'Shift':0
     ck←⍵.CtrlKey≡⊂'true'
     l←⍵.Document.AutoComplete
     a←l.Active
     (~a)∧k≡'Enter':OnEnter ⍵
     (~a)∧k≡'ArrowDown':¯1 History ⍵
     (~a)∧k≡'ArrowUp':1 History ⍵
     ⍝ ck∧k≡'Enter':(⍎e.OnCtrlEnter)⍵ ⍝ WHERE DID WE USE THIS?
     a∧k≡'ArrowRight':SelectItem ⍵    ⍝ Not like VS Code
     ⍝a∧k≡'Enter':Hide l
     a∧k≡'Tab':SelectItem ⍵
     a∧k≡'ArrowDown':ResetCursor ⍵⊣1 OnArrowUpDown ⍵
     a∧k≡'ArrowUp':ResetCursor ⍵⊣¯1 OnArrowUpDown ⍵
     ~(⊂k)∊,¨e.TriggerKeys:Hide l
     rc st←GetStartText ⍵
     rc:Hide l
     n←≢st
     d←+/st=p.Separator
     b←(d=p.Depth)∧(⊂st)≡¨n↑¨p.FQN
     ~∨/b:Hide l
     l.(Mask StartText)←b st
     c←{{A.New'li'⍵}{A.New'span'⍵}¨⍵}¨b/↓⍉↑p.Name p.Type
     c.id←⍵.Document A.NextIds≢c
     c.class←⊂''
     (0⊃c).class←'active-item'
     _←l SetPosition ⍵
     _←l A.SetProperty'style.font'⍵.Font
     _←l A.SetInnerHTML c
     _←l A.ExecuteOnElement'showPopover()'
     0
 }
