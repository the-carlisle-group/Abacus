 OnKeyDown←{

     k←⍵.Key
     ⎕←'Key: ',k
     e←⍵.CurrentTarget ⍝ Input element
     p←e.Options
     k≡'Shift':0
     ck←⍵.CtrlKey≡⊂'true'
     l←GetAutoCompleteElement e
     e.AutoCompleteElement←l
     a←0<≢AutoCompleteElements l
     (~a)∧k≡'Enter':OnEnter ⍵
     (~a)∧k≡'ArrowDown':¯1 History ⍵
     (~a)∧k≡'ArrowUp':1 History ⍵
     ⍝ ck∧k≡'Enter':(⍎e.OnCtrlEnter)⍵ ⍝ WHERE DID WE USE THIS?
     a∧k≡'ArrowRight':StopNextKeyStroke ⍵⊣SelectItem ⍵   ⍝ Not like VS Code
     ⍝ a∧k≡'Enter':StopNextKeyStroke ⍵⊣1 SelectItem ⍵
     a∧k≡'Tab':StopNextKeyStroke ⍵⊣1 SelectItem ⍵
     a∧k≡'Escape':e A.ExecuteOnElement'stopPropagation()'⊣StopNextKeyStroke ⍵⊣1 Clear e
     a∧k≡'ArrowDown':ResetCursor ⍵⊣1 OnArrowUpDown ⍵
     a∧k≡'ArrowUp':ResetCursor ⍵⊣¯1 OnArrowUpDown ⍵
     ~(⊂k)∊,¨e.TriggerKeys:Clear e
     rc st←GetStartText ⍵
     rc:Clear e
     n←≢st
     d←+/st=p.Separator
     b←(d=p.Depth)∧(⊂st)≡¨n↑¨p.FQN
     ~∨/b:Clear e
     l.(Mask StartText)←b st
     c←{{A.New'li'⍵}{A.New'span'⍵}¨⍵}¨b/↓⍉↑p.Name p.Type
     c.id←⍵.Document A.NextIds≢c
     c.class←⊂''
     (0⊃c).class←'active-item'
     _←l SetPosition ⍵
     _←l A.SetProperty'style.font'⍵.Font
     _←l A.SetInnerHTML c
     t←⍵.CurrentTarget A.GetNearest'table'
     0=t:0
     t.AutoCompleteActive←1
     0
 }
