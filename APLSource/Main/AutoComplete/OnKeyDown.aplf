 OnKeyDown←{
     ⍝ ⎕←'OnKeyDown in AutoComplete'
     k←⍵.Key
     e←⍵.CurrentTarget ⍝ Input element
     k≡'Shift':0
     ck←⍵.CtrlKey≡⊂'true'
     l←⍵.Document.AutoCompleteElement
     a←0<≢AutoCompleteElements l
     (~a)∧k≡'Enter':OnEnter ⍵
     (~a)∧k≡'ArrowDown':¯1 History ⍵
     (~a)∧k≡'ArrowUp':1 History ⍵
     ⍝ ck∧k≡'Enter':(⍎e.OnCtrlEnter)⍵ ⍝ WHERE DID WE USE THIS?
     ⍝ a∧k≡'ArrowRight':SelectItem ⍵   ⍝ Not like VS Code
     a∧k≡'Enter':StopPropagation ⍵⊣1 SelectItem ⍵
     a∧k≡'Tab':StopPropagation ⍵⊣1 SelectItem ⍵
     a∧k≡'Escape':StopPropagation ⍵⊣1 Clear e
     a∧k≡'ArrowDown':ResetCursor ⍵⊣1 OnArrowUpDown ⍵
     a∧k≡'ArrowUp':ResetCursor ⍵⊣¯1 OnArrowUpDown ⍵
     ~(⊂k)∊,¨e.TriggerKeys:Clear l
     rc st←GetStartText ⍵
     rc:Clear e
     i←e.AutoCompleteItems
     b←((≢st)↑¨i)∊⊂st
     ~∨/b:Clear e
     l.(Mask StartText)←b st
     c←##.New¨{'div'⍵}¨b/i
     c.id←⍵.Document ##.NextIds≢c
     c.class←⊂''
     (0⊃c).class←'active-item'
     _←SetPosition ⍵
     _←l ##.SetProperty'style.font'⍵.Font
     _←l ##.SetInnerHTML c
     t←⍵.CurrentTarget A.GetNearest'table'
     0=t:0
     t.AutoCompleteActive←1
     0
 }
