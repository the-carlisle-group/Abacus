 Init←{
     ⍺←0
     g←⍵
     t←g.Table
     ⍺∧t.Init:1
     0=≢t.Columns:1
     t.Columns←DefaultColumnProperties t
     c←t.Columns
     r←(Align:'r' ⋄ Class:'r' ⋄ SortDirection:0)    ⍝ For row numbers
     t.RowNumberColumn←r
     a←r,c   ⍝ All columns, inlcuding row numbers
     _←g InitDisplayColumns t.DisplayColumns
     _←g InitRowMask t.RowMask
     c.HeaderValues←↓↑,¨⊆¨c.HeaderValues  ⍝ V of V, same length
     t.HeaderRowCount←≢⊃c.HeaderValues
     t.HeaderRowTitles←{⍵↑⊆t.HeaderRowTitles,⍵⍴⊂''}t.HeaderRowCount
     t.HeaderReadOnly←{⍵↑t.HeaderReadOnly,⍵⍴1}t.HeaderRowCount
     ⍝ For now: Total header height....
     t.HeaderHeight←t.HeaderRowCount×3×t.FontSize ⍝ Number of header rows ...
     t.RowHeight←2×t.FontSize
     _←g ##.SetDynamicCSS DynamicCSS t
     t.DataSize←≢¨t.RowIndices t.ColumnIndices
     c.MaxLength←1⊃¨2↑¨⍴¨c.Value
     a.Width←t ComputeWidths 0
     a.ColumnTitle←(t.HeaderRowTitles),c.HeaderValues
     t.FixedColumns←t.RowNumbers+t.FreezeColumns
     t.WindowIndex←0,t.FreezeColumns
     t.ActiveCell←0 0
     t.Selection←2⍴⊂t.ActiveCell
     a.HeaderStyle←ComputeHeaderStyles a
     a.HeaderClass←ComputeHeaderClass a
     a.ColumnClass←(a.Class),¨' ',¨'a',¨a.Align
     t.Highlight←{0≡⍵:⍵ ⋄ ⍵↑⍨-0 t.RowNumbers+⍴⍵}t.Highlight ⍝ Move into a (column spaces)
     t.WindowPadding←3 5
     t.Init←1
     0
 }
