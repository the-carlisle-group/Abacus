 GetContent←{
     t←⍺
     oi j←t.WindowIndex+⍳¨(t.WindowSize+t.WindowPadding)⌊t.DataSize-t.WindowIndex
     i←t.RowIndices[oi]
     k←(⍳⍺.FreezeColumns),j
     q←(⍺.RowNumbers/0),k+⍺.RowNumbers
     v←⍺ GetFormattedValues i k oi
     nr nc←⍴v
     o←⍺.OptiTable
     h←o.Header
     h.Values←⍺.ColumnTitles[;q]
     h.Cells.style←⍺.HeaderStyles[q]
     h.Cells.class←(t.HeaderRowCount nc)⍴⍺.HeaderClass[q]
     _←{
         t.TableCell≡¯1 ¯1:0
         ∧/t.TableCell≥0:0
         r c←t.TableCell
         r←r+t.HeaderRowCount
         h.Cells.class[⊂r c]←⊂((⊂r c)⊃h.Cells.class),' active-cell'
         0
     }0
     h.Cells.id←(t.HeaderRowCount nc)↑t.HeaderCells.id
     o.Body.Values←v
     o.Body.Cells.id←(nr nc)↑t.BodyCells.id
     bc←t Highlight i q(nr nc⍴⍺.ColumnClass[q])
     bc←t Selection bc
     _←{
         ∨/t.TableCell<0:0
         0=≢bc:0
         bc[⊂t.TableCell]←⊂((⊂t.TableCell)⊃bc),' active-cell'
         0
     }0
     o.Body.Cells.class←bc
     ⍺.WindowIndex[1]≤⍺.FreezeColumns:o
     bc[;⍺.FixedColumns]←bc[;⍺.FixedColumns],¨⊂' more-to-left'
     o.Body.Cells.class←bc
     o
 }
