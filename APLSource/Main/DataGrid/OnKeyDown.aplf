 OnKeyDown←{
     t←⍵.CurrentTarget
     t.IgnoreNextKey:t.IgnoreNextKey←0
     t.AutoCompleteActive:0
     m←t.CellMode
     k←⍵.Key
     ⍝ ⎕←⍵.Key
     k⊂⍛∊'Control' 'Shift' 'Alt' 'Process':0
     b←⍵.(CtrlKey ShiftKey AltKey)∊⊂⊂'true'
     p←k,⍨∊b/'Ctrl' 'Shift' 'Alt'
     ⍝ ⎕←p
     p≡'Escape':Escape t
     p≡'Enter':t Move t.ActiveCell+1 0
     p≡'ShiftEnter':t Move t.ActiveCell-1 0
     p≡'Tab':t Move t.ActiveCell+0 1
     p≡'ShiftTab':t Move t.ActiveCell-0 1
     m=2:0
     p≡'ArrowRight':t Move t.ActiveCell+0 1
     p≡'ArrowLeft':t Move t.ActiveCell-0 1
     p≡'ArrowUp':t Move t.ActiveCell-1 0
     p≡'ArrowDown':t Move t.ActiveCell+1 0
     p≡'CtrlArrowUp':t Move 0,t.ActiveCell[1]
     p≡'CtrlArrowDown':t Move(t.DataSize[0]-1),t.ActiveCell[1]
     p≡'CtrlArrowLeft':t Move t.ActiveCell[0],0
     p≡'CtrlArrowRight':t Move t.ActiveCell[0],t.DataSize[1]-1
     p≡'Home':t Move t.ActiveCell[0],0
     p≡'End':t Move t.ActiveCell[0],t.DataSize[1]-1
     p≡'CtrlHome':t Move 0 0
     p≡'CtrlEnd':t Move t.DataSize-1
     p≡'PageUp':MovePageUp t
     p≡'PageDown':MovePageDown t
     p≡'AltPageUp':MovePageLeft t
     p≡'AltPageDown':MovePageRight t
     p≡'ShiftArrowRight':t Select 0 1
     p≡'ShiftArrowLeft':t Select 0 ¯1
     p≡'ShiftArrowUp':t Select ¯1 0
     p≡'ShiftArrowDown':t Select 1 0
     p≡'ShiftHome':t SelectTo 0,⍨⊃SelectionCorner t
     p≡'ShiftEnd':t SelectTo(⊃SelectionCorner t),t.DataSize[1]-1
     p≡'CtrlShiftArrowUp':t SelectTo 0,1⊃SelectionCorner t
     p≡'CtrlShiftArrowDown':t SelectTo(t.DataSize[0]-1),1⊃SelectionCorner t
     p≡'CtrlShiftHome':t SelectTo 0 0
     p≡'CtrlShiftEnd':t SelectTo t.DataSize-1
     p≡'ShiftPageUp':SelectPageUp                  ⍝ TODO
     p≡'ShiftPageDown':SelectPageDown              ⍝ TODO
     p≡'CtrlA':t ExtendSelectAll 0
     p≡'CtrlShiftS':t Sort 0
     p≡'CtrlShiftF':t Filter 0
     p≡'CtrlShiftF3':t Filter 0
     p≡'Ctrl ':t ExtendSelectColumn 0
     p≡'Shift ':t ExtendSelectRow 0
     p≡'F6':t FreezeColumns 0
     p≡'F5':t.Parent Resize 1
     p≡'Ctrl+':t.Parent Resize 1⊣t.FontSize←32⌊t.FontSize+2
     p≡'Ctrl-':t.Parent Resize 1⊣t.FontSize←8⌈t.FontSize-2
     (⊂p)∊'Ctrlc' 'CtrlC':t Copy 0
     t.ReadOnly:0
     (⊂p)∊'Ctrlx' 'CtrlX':t Cut 0
     (⊂p)∊'Ctrlv' 'CtrlV':t Paste 0
     p≡'Delete':t Delete 0
     p≡'F2':⍵ StartEditMode t
     p≡'AltArrowDown':⍵ StartEditMode t
     m=1:0
     k≡,' ':ToggleCheckBox t
     (1=≢k)∧(⊃k)∊⎕D,(⊣,⎕C)⎕A:k StartEnterMode t
     p≡'CtrlInsert':InsertRow t                    ⍝ Broken!
     p≡'F9':InsertRow t
     p≡'CtrlDelete':DeleteRow t
     p≡'CtrlShiftInsert':InsertColumn t            ⍝ TODO!
     p≡'CtrlShiftF10':DeleteColumn t
     p≡'CtrlShiftD':DeleteColumn t
     p≡'CtrlF2':RenameColumn t                     ⍝ Needs Work!
     p≡'ShiftAltArrowLeft':¯1 MoveColumn t
     p≡'ShiftAltArrowRight':1 MoveColumn t
     p≡'ShiftAltArrowUp':MoveRowUp t
     p≡'ShiftAltArrowDown':MoveRowDown t
     0
 }
