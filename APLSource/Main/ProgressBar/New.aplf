 New←{
     ⍺←0
     w←(
         'Title' 'Progress'
         'Iterations' 0
         'Cancel' 1
         'Pause' 1
         'Truncate' 0
         'UpdatePeriod' 1
         '_width' '30rem'
         'Status' ''
         'LanguageTable'(LanguageTable'')
     )A.Default ⍵
     w.Iterations←(2=≡w.Status)⊃w.Iterations(≢w.Status)
     s←A.New'p'
     s.id←'progress-bar-status'
     p←A.New'progress'
     p.id←'progress'
     p.max←w.Iterations
     p.value←'0'
     fp←A.New'p'(w A.Translate'Paused')
     fp.id←'progress-paused-indicator'
     fp.class←'pulse invisible'
     w.MainContent←s,((0<w.Iterations)/p),fp
     b←w.(Cancel Pause Truncate)
     w.Buttons←b/w A.Translate'Cancel' 'Pause' 'Truncate'
     w.OnClose←A.FQP'OnClose'
     w.OnClick←A.FQP¨b/'OnCancel' 'OnPause' 'OnTruncate'
     d←⍺ A.DialogBox.New w
     d.id←'progress-bar'
     d.(Iteration Result)←¯1 ⍬
     d.(Exit Paused Canceled Truncated)←0
     f←2⊃d.Content
     f.Content.id←b/'progress-'∘,¨'cancel' 'pause' 'truncate'
     (d f).Unqueued←'close' 'click'
     d.PauseToken←⎕TID
     d
 }
