 z←{y}OnProgressBar1 x;d;i;p;r
 ⍝ Sample of conventional progress bar in Trad function
 ⍝ With explicit calls to Begin/Update/End
 ⍝ This is deterministic, we know we are going to loop 25 times
 ⍝ ***********************************************************
 ⍝ Put up "modal" form with pause button, return component:
 d←x.Document
 p←d A.ProgressBar.Begin('Title' 'Progress')('Iterator'(⍳25))('Truncate' 1)
 ⍝ Start up a loop:
 :For i :In ⍳25
     ⍝ Update the progress bar with a new status and check if Pause has been clicked.
     ⍝ It is good to update first, before doing work, though you don't have to.
     ⍝ If Pause is clicked, puts up a a (truely) modal ConfirmBox
     ⍝ with Cancel, Resume, and (optionally) Truncate buttons.
     r←p A.ProgressBar.Update'Doing iteration: ',⍕1+i
     :If r≢'Resume' ⋄ :Leave ⋄ :EndIf
     ⍝ Do some work, accumulate results, etc.
     ⎕DL 0.2
 :EndFor
 ⍝ Close the progress bar
 p A.ProgressBar.End''
 ⍝ Do any clean up
 ⍝ Analyze r in whatever way is convenient,
 ⍝ and take appropriate action, for example:
 :Select r
 :Case 'Cancel'
     z←d A.Alert'You canceled the operation.'
 :Case 'Truncate'
     z←d A.Alert'You truncated the operation.'
 :Case 'Resume'
     z←d A.Alert'The operation completed.'
 :EndSelect
