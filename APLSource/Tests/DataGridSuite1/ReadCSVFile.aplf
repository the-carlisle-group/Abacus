 ReadCSVFile←{
     ⍝ ⍺ ←→ Limit rows, Limit Columns
     ⍝ ⍵ ←→ FQP file name
     ⍝ ← ←→ Namespace with Names, Values, etc.
     rl cl←2↑⍺,0
     o←'Invert' 1
     v n←(⎕CSV⍠o)⍵'' 1 1
     v←rl{⍺=0:⍵ ⋄ ⍺↑⍵}¨v
     n v←cl{⍺=0:⍵ ⋄ ⍺↑⍵}¨n v
     p←ConvertType¨v
     c←≢n
     t←⎕NS''
     cc←⎕NS¨c⍴⊂''
     t.Columns←cc
     cc.Value←p.Value
     cc.Name←n
     cc.Type←p.Type
     cc.Options←⊂''
     ~∨/n∊⊂'birthCity':t

     j←n⍳⊂'birthYear'
     (j⊃cc).FormatString←'LI4'
     (j⊃cc).OnValidate←'{((⍵≥1800)∧⍵≤2100) ⍵ ''birthYear must be between 1800 and 2100''}'

     j←n⍳⊂'birthCity'
     (j⊃cc).Options←∪#.Abacus.Main.ntbs(j⊃t.Columns).Value
     j←n⍳⊂'birthCountry'
     (j⊃cc).Options←∪#.Abacus.Main.ntbs(j⊃t.Columns).Value
     j←n⍳⊂'birthState'
     (j⊃cc).Options←∪#.Abacus.Main.ntbs(j⊃t.Columns).Value
     t
 }
